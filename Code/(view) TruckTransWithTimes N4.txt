USE [TOSGateway]
GO

/****** Object:  View [dbo].[vwTruckTransWithTimes]    Script Date: 6/20/2019 2:06:05 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO





/*******************************************
Author:  T. Reilly, Advent Intermodal Solutions
Date:    11/07/2016
Purpose: This view contains truck transaction
         information including start/end times
		 based on the TIR (document), which is
		 agreed as how turn times are to be 
		 based.  This view is the basis for the
		 view vwTruckTransWithTurnTime, which
		 is used for turn time reports.

-- Modified 11/7/2018 by T. Reilly
-- Added dual_trans_gkey, dual_trans_nbr, 
-- ctr_gross_weight, eqo_nbr, unit_gkey and
-- appointment_nbr columns; did outer join
-- to unit tables as we can have truck transactions
-- with no containers

Modified 12/03/2018 by A.Chernyak
for performance reasons and to avoid dups

-- Modified 3/6/2019 by T. Reilly
-- Joined ref_equipment to road_ar_truck_transactions.ctr_id
-- as inv_ar_unit.id may be null in the outer join.
--
-- Modified 3/22/2019 by T. Reilly
-- Added column ctr_ticket_pos_id and used that to
-- determine wheeled/grounded per LSA specs.  
*******************************************/ 

CREATE VIEW [dbo].[vwTruckTransWithTimes] 
AS 
SELECT a.[gkey]
      ,a.[transaction_nbr]
	  ,a.[eq_nbr]
	  ,SUBSTRING(a.[eqsz_id], 1, 10) AS eqsz_id
	  ,SUBSTRING(a.[eqht_id], 1, 10) AS eqht_id
	  ,SUBSTRING(a.[eqtp_id], 1, 10) AS eqtp_id
	  ,a.[truck_entered]
	  ,a.[truck_exited]
	  ,a.[in_time]
	  ,a.[chs_nbr]
	  ,a.[truck_license_nbr]
	  ,SUBSTRING(a.[truck_entered_lic], 1, 100) AS truck_entered_lic
	  ,SUBSTRING(a.[date_id], 1, 8) AS date_id
	  ,a.[line_id]
	  ,a.[trans_type]
	  ,a.[trans_type_text]
	  ,a.[trucker_id]
	  ,a.[trucker_name]
	  ,a.[location_code]
	  ,a.[ship_id]
	  ,a.[voy_nbr]
	  ,a.[voy_ata]
	  ,CASE
	       WHEN UPPER(SUBSTRING(a.[ctr_ticket_pos_id], 1, 1)) BETWEEN 'A' AND 'Z' THEN
		       'G'
		   WHEN SUBSTRING(a.[ctr_ticket_pos_id], 1, 1) BETWEEN '0' AND '9' THEN
		       'W'
		   ELSE 
		       NULL
	   END AS wheeled_grounded
	  ,a.[group_id]
	  ,a.[trouble_fl]
	  ,a.[haz_fl]
	  ,a.[oog_fl]
	  ,a.[reefer_fl]
	  ,a.[pod]
	  ,a.[pol]
	  ,a.[start_time]
	  ,a.[end_time]
	  ,(CAST(DATEDIFF(SECOND, a.[start_time], a.[end_time]) AS FLOAT) / 60) / (IIF(a.[dual_line] NOT IN ('N', a.[line_id]), 2, 1)) AS elapsed_time
	  ,CAST(DATEDIFF(SECOND, a.[start_time], a.[end_time]) AS FLOAT) / 60  AS full_elapsed_time
	  ,a.[trouble_start]
	  ,ISNULL(CASE
				  WHEN a.[trouble_fl] = 'N' THEN 0
		  		  WHEN a.[trans_type] LIKE 'R%' AND
					   a.[dual_type] IN ('N', 'DC') THEN
					  0
			      ELSE CAST(DATEDIFF(SECOND, a.[trouble_start], (SELECT MIN(rd7.[created])
							  									 FROM   [dbo].[vwRoadDocuments]  AS rd7 WITH (NOLOCK)
					    										 WHERE  rd7.[tran_gkey] = a.[gkey]
																 AND    rd7.[doc_type_id] <> 'ERROR'
																 AND    rd7.[created] > a.[trouble_start])) AS FLOAT) / 60
			  END,
			  0) / (IIF(a.[dual_line] NOT IN ('N', a.[line_id]), 2, 1)) AS trouble_time
	  ,ISNULL(CASE
				  WHEN a.[trouble_fl] = 'N' THEN 0
			   	  WHEN a.[trans_type] LIKE 'R%' AND
					   a.[dual_type] IN ('N', 'DC') THEN
					  0
				  ELSE CAST(DATEDIFF(SECOND, a.[trouble_start], (SELECT MIN(rd7.[created])
																 FROM   [dbo].[vwRoadDocuments]  AS rd7 WITH (NOLOCK)
					     										 WHERE  rd7.[tran_gkey] = a.[gkey]
																 AND    rd7.[doc_type_id] <> 'ERROR'
															 	 AND    rd7.[created] > a.[trouble_start])) AS FLOAT) / 60
			  END,
			  0) AS full_trouble_time
	  ,CASE
	       WHEN (SELECT [DEFAULT_VAL]
				 FROM   [dbo].[TOS_PARAMETER]
				 WHERE  [PARAMETER_CD] = 'THISTERM') = 'LAX' THEN
			   CASE -- Comment this CASE statement out if using this for NWK N4
				   WHEN DATEPART(HOUR, a.[start_time]) BETWEEN 7 AND 16 THEN
				       CAST(1 AS SMALLINT)
				   WHEN DATEPART(HOUR, a.[start_time]) BETWEEN 3 AND 6 THEN
				   	   CAST(3 AS SMALLINT)
				   ELSE
					   CAST(2 AS SMALLINT)
			   END
		   ELSE
			   CAST(IIF(DATEPART(HOUR, a.[start_time]) BETWEEN 6 AND 18, 1, 2) AS SMALLINT)
	   END AS shift_nbr
	  ,CASE
		   WHEN FORMAT(a.[start_time], 'HHmm') BETWEEN '0700' AND '0759' AND
				FORMAT(a.[end_time], 'HHmm') NOT BETWEEN '0700' AND '0759' THEN
			   DATEDIFF(MINUTE, a.[start_time], DATEADD(HOUR, 8, a.[trunc_truck_entered]))
		   WHEN FORMAT(a.[end_time], 'HHmm') BETWEEN '0700' AND '0759' AND
				FORMAT(a.[start_time], 'HHmm') NOT BETWEEN '0700' AND '0759' THEN
			   DATEDIFF(MINUTE, DATEADD(HOUR, 7, a.[trunc_truck_entered]), a.[end_time])
		   WHEN FORMAT(a.[start_time], 'HHmm') BETWEEN '1200' AND '1259' AND
				FORMAT(a.[end_time], 'HHmm') NOT BETWEEN '1200' AND '1259' THEN
			   DATEDIFF(MINUTE, a.[start_time], DATEADD(HOUR, 13, a.[trunc_truck_entered]))
		   WHEN FORMAT(a.[end_time], 'HHmm') BETWEEN '1200' AND '1259' AND
				FORMAT(a.[start_time], 'HHmm') NOT BETWEEN '1200' AND '1259' THEN
			   DATEDIFF(MINUTE, DATEADD(HOUR, 12, a.[trunc_truck_entered]), a.[end_time])
		   WHEN FORMAT(a.[start_time], 'HHmm') BETWEEN '1700' AND '1759' AND
				FORMAT(a.[end_time], 'HHmm') NOT BETWEEN '1700' AND '1759' THEN
			   DATEDIFF(MINUTE, a.[start_time], DATEADD(HOUR, 18, a.[trunc_truck_entered]))
		   WHEN FORMAT(a.[end_time], 'HHmm') BETWEEN '1700' AND '1759' AND
				FORMAT(a.[start_time], 'HHmm') NOT BETWEEN '1700' AND '1759' THEN
			   DATEDIFF(MINUTE, DATEADD(HOUR, 17, a.[trunc_truck_entered]), a.[end_time])
		   WHEN FORMAT(a.[start_time], 'HHmm') BETWEEN '2200' AND '2259' AND
				FORMAT(a.[end_time], 'HHmm') NOT BETWEEN '2200' AND '2259' THEN
			   DATEDIFF(MINUTE, a.[start_time], DATEADD(HOUR, 23, a.[trunc_truck_entered]))
		   WHEN FORMAT(a.[end_time], 'HHmm') BETWEEN '2200' AND '2259' AND
				FORMAT(a.[start_time], 'HHmm') NOT BETWEEN '2200' AND '2259' THEN
			   DATEDIFF(MINUTE, DATEADD(HOUR, 22, a.[trunc_truck_entered]), a.[end_time])
		   ELSE 
			   0
	   END AS break_time
	  ,a.[dual_type]
	  ,a.[dual_line]
	  ,IIF(a.[dual_type] = 'N', 'N', a.[dual_reefer]) AS dual_reefer
      ,CASE
		   WHEN a.[dual_type] = 'N' THEN
		       'N'
	       WHEN a.[dual_type] <> 'N' AND UPPER(SUBSTRING(a.[dual_ctr_ticket_pos_id], 1, 1)) BETWEEN 'A' AND 'Z' THEN 
		       'G'
		   WHEN a.[dual_type] <> 'N' AND SUBSTRING(a.[dual_ctr_ticket_pos_id], 1, 1) BETWEEN '0' AND '9' THEN
		       'W'
		   ELSE
		       NULL
	   END AS dual_wheeled_grounded
	  ,CASE
		   WHEN a.[dual_type] = 'N' THEN
			   'N'
		   WHEN ((a.[trans_type] IN ('RE', 'RI', 'RM') AND a.[dual_type] IN ('DE', 'DI', 'DM')) OR
			  	 (a.[trans_type] IN ('DE', 'DI', 'DM') AND a.[dual_type] IN ('RE', 'RI', 'RM'))) THEN
			   'Y'
		   WHEN ((a.[trans_type] IN ('RE', 'RI', 'RM') AND a.[dual_type] IN ('N', 'DC')) OR
			 	 (a.[trans_type] = 'RC' AND a.[dual_type] IN ('DE', 'DI', 'DM')) OR
			  	 (a.[trans_type] IN ('DE', 'DI', 'DM') AND a.[dual_type] IN ('RE', 'RI', 'RM')) OR
			  	 (a.[trans_type] = 'DC' AND a.[dual_type] IN ('RE', 'RI', 'RM'))) THEN
			   'N'
		   ELSE
			   'N'
	   END AS dual_move_fl
	  ,a.[dual_trans_gkey]
	  ,a.[dual_trans_nbr]
	  ,a.[dual_ctr_ticket_pos_id]
	  ,a.[entry_lane]
	  ,a.[exit_lane]
	  ,a.[truck_entered_prior_day]
	  ,a.[truck_entered_day]
	  ,a.[trunc_truck_entered]
	  ,a.[ctr_pos_id]
	  ,a.[ctr_ticket_pos_id]
	  ,a.[ctr_gross_weight]
	  ,a.[eqo_nbr]
	  ,a.[unit_gkey]
	  ,a.[appointment_nbr]
FROM   (
		SELECT tt.[gkey]
			  ,tt.[nbr] AS transaction_nbr
			  ,ISNULL(tt.[ctr_id], u.[id]) AS eq_nbr
			  ,SUBSTRING(et.[nominal_length], 4, LEN(et.[nominal_length]) - 3) AS eqsz_id
			  ,et.[iso_group] AS eqtp_id
			  ,SUBSTRING(et.[nominal_height], 4, LEN(et.[nominal_height]) - 3) AS eqht_id
			  ,td.[entered_yard] AS truck_entered
			  ,td.[exited_yard] AS truck_exited
			  ,ISNULL(uv.[time_in], td.[entered_yard]) AS in_time
			  ,tt.[chs_id] AS chs_nbr
			  ,td.[truck_license_nbr]
			  ,CONCAT(FORMAT(td.[entered_yard], 'yyyyMMddHHmmss'), td.[truck_license_nbr]) AS truck_entered_lic
			  ,CASE -- LAX 2nd shift for the day ends at 3 AM of the following day
				   WHEN (SELECT [DEFAULT_VAL]
					     FROM   [dbo].[TOS_PARAMETER]
						 WHERE  [PARAMETER_CD] = 'THISTERM') = 'LAX' AND 
					    DATEPART(HOUR, td.[entered_yard]) < 3 THEN 
					   FORMAT(DATEADD(DAY, -1, td.[entered_yard]), 'yyyyMMdd')
				   ELSE
					   FORMAT(td.[entered_yard], 'yyyyMMdd') 
			   END AS date_id
			  ,tt.[line_id]
			  ,tt.[sub_type] AS trans_type
			  ,CASE tt.[sub_type]
				   WHEN 'RM' THEN 'Receive Empty'
				   WHEN 'RE' THEN 'Receive Export'
				   WHEN 'RI' THEN 'Receive Import'
				   WHEN 'RC' THEN 'Receive Chassis'
				   WHEN 'DM' THEN 'Deliver Empty'
				   WHEN 'DE' THEN 'Deliver Export'
				   WHEN 'DI' THEN 'Deliver Import'
				   WHEN 'DC' THEN 'Deliver Chassis'
				   ELSE tt.[sub_type]
			   END AS trans_type_text
			  ,tt.[trkco_id] AS trucker_id
			  ,tc.[name] AS trucker_name
			  ,(SELECT [DEFAULT_VAL]
				FROM   [dbo].[TOS_PARAMETER]
				WHERE  [PARAMETER_CD] = 'THISTERM') AS location_code
			  ,IIF(uv.[arrive_pos_loctype] = 'VESSEL', vsl.[id], NULL) AS ship_id
			  ,IIF(uv.[arrive_pos_loctype] = 'VESSEL', vvd.[ib_vyg], NULL) AS voy_nbr
			  ,IIF(uv.[arrive_pos_loctype] = 'VESSEL', icv.[ata], NULL) AS voy_ata
--------------------
			  --,ISNULL(memax.[fm_pos_bin], memin.[to_pos_bin])  AS from_pos_gkey
			  --,ISNULL((SELECT me1.[fm_pos_bin] 
					--   FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me1 WITH (NOLOCK)
					--   WHERE  me1.[mve_gkey] = (SELECT MAX(me2.[mve_gkey])
					--							FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me2 WITH (NOLOCK)
				 --       						WHERE  me2.[ufv_gkey] = uv.[gkey]
					--							AND    me2.[fm_pos_loctype] = 'YARD'))
					-- ,(SELECT me3.[to_pos_bin]
					--   FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me3 WITH (NOLOCK)
					--   WHERE  me3.[mve_gkey] = (SELECT MIN(me4.[mve_gkey])
					--							FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me4 WITH (NOLOCK)
					--							WHERE  me4.[ufv_gkey] = uv.[gkey]
					--							AND    me4.[to_pos_loctype] = 'YARD'))) AS from_pos_gkey
----------------------
			  ,tt.[group_id]
			  ,IIF(ISNULL(tt.[had_trouble], 0) = 1, 'Y', 'N') AS trouble_fl
			  ,IIF(ISNULL(tt.[is_hazard], 0) = 1, 'Y', 'N') AS haz_fl
			  ,IIF(ISNULL(tt.[is_oog], 0) = 1, 'Y', 'N') AS oog_fl
			  ,IIF(tt.[temp_required] IS NOT NULL OR 
				   (tt.[ctr_freight_kind] = 'MTY' AND et.[iso_group] = 'RE'), 'Y', 'N') AS reefer_fl
			  ,pod.[id] AS pod
			  ,pol.[id] AS pol
			  ,ISNULL((SELECT MIN(rd1.[created])
				       FROM   [SparcsN4R].[dbo].[road_documents]   AS rd1 WITH (NOLOCK)
				       WHERE  rd1.[tran_gkey] = (SELECT MIN(tt2.[gkey])
									             FROM  [SparcsN4R].[dbo].[road_truck_transactions]  AS tt2 WITH (NOLOCK)
										         JOIN  [SparcsN4R].[dbo].[road_truck_visit_details] AS td2 WITH (NOLOCK) ON td2.[tvdtls_gkey] = tt2.[truck_visit_gkey] AND
																										                    td2.[truck_license_nbr] = td.[truck_license_nbr] AND
																											                td2.[entered_yard] = td.[entered_yard]
										         WHERE tt2.[status] = tt.[status])) 
		             ,td.[entered_yard]) AS start_time
			  ,CASE
				   WHEN tt.[sub_type] LIKE 'D%' THEN 
					   (SELECT MAX(rd2.[created])
						FROM  [SparcsN4R].[dbo].[road_documents]  AS rd2 WITH (NOLOCK)
						WHERE  rd2.[tran_gkey] = tt.[gkey])
				   ELSE ISNULL((SELECT MAX(rd3.[created])
							    FROM   [SparcsN4R].[dbo].[road_documents]           AS rd3 WITH (NOLOCK)
								JOIN   [SparcsN4R].[dbo].[road_truck_transactions]  AS tt3 WITH (NOLOCK) ON tt3.[gkey] = rd3.[tran_gkey] AND
																							                tt3.[status] = tt.[status] AND
																								            LEFT(tt3.[sub_type], 1) <> LEFT(tt.[sub_type], 1)
								JOIN   [SparcsN4R].[dbo].[road_truck_visit_details] AS td3 WITH (NOLOCK) ON td3.[tvdtls_gkey] = tt3.[truck_visit_gkey] AND
																								            td3.[entered_yard] = td.[entered_yard] AND
																								            td3.[truck_license_nbr] = td.[truck_license_nbr])
							    ,(SELECT MAX(rd4.[created])
								  FROM   [SparcsN4R].[dbo].[road_documents]   AS rd4 WITH (NOLOCK)
								  WHERE  rd4.[tran_gkey] = tt.[gkey]))
			   END AS end_time
			  ,(SELECT MIN(rd6.[created])
				FROM   [SparcsN4R].[dbo].[road_documents]      AS rd6 WITH (NOLOCK)
				JOIN   [SparcsN4R].[dbo].[road_document_types] AS rt1 WITH (NOLOCK) ON rt1.[gkey] = rd6.[doctype_gkey] AND
						                                                               rt1.[id] = 'ERROR'
				WHERE  rd6.[tran_gkey] = tt.[gkey] AND tt.[had_trouble] = 1) AS trouble_start

			  ,ISNULL(dual.[sub_type],'N') AS dual_type
			  ,ISNULL(dual.[line_id],'N') AS dual_line
			  ,ISNULL((SELECT IIF(dual.[temp_required] IS NOT NULL OR
									  (dual.[ctr_freight_kind] = 'MTY' AND et2.[iso_group] = 'RE'), 'Y', 'N')
					   FROM   [SparcsN4R].[dbo].[inv_unit]                  AS u2  WITH (NOLOCK) 
					   JOIN   [SparcsN4R].[dbo].[ref_equipment]             AS eq2 WITH (NOLOCK) ON eq2.[gkey] = u2.[eq_gkey]
					   JOIN   [SparcsN4R].[dbo].[ref_equip_type]            AS et2 WITH (NOLOCK) ON et2.[gkey] = eq2.[eqtyp_gkey]
					   WHERE u2.[gkey] = dual.[unit_gkey])
					  ,'N') AS dual_reefer
--------------------
			  --,ISNULL(dualmemax.[fm_pos_bin], dualmemin.[to_pos_bin])  AS dual_from_pos_gkey
			  --,ISNULL((SELECT me3.[fm_pos_bin]
					--	FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me3 WITH (NOLOCK)
					--	WHERE  me3.[mve_gkey] = (SELECT MAX(me4.[mve_gkey])
					--							 FROM   [SparcsN4R].[dbo].[inv_move_event]            AS me4 WITH (NOLOCK)
					--							 JOIN   [SparcsN4R].[dbo].[inv_unit_fcy_visit]        AS uv2 WITH (NOLOCK) ON uv2.[gkey] = me4.[ufv_gkey] AND dual.[unit_gkey] = uv2.[unit_gkey]
					--							 WHERE  me4.[fm_pos_loctype] = 'YARD'))
					--   ,(SELECT me5.[to_pos_bin]
					--	 FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me5 WITH (NOLOCK)
					--	 WHERE  me5.[mve_gkey] = (SELECT MIN(me6.[mve_gkey])
					--							  FROM   [SparcsN4R].[dbo].[inv_move_event]            AS me6 WITH (NOLOCK)
					--							  JOIN   [SparcsN4R].[dbo].[inv_unit_fcy_visit]        AS uv3 WITH (NOLOCK) ON uv3.[gkey] = me6.[ufv_gkey] AND dual.[unit_gkey] = uv3.[unit_gkey]
					--							  WHERE  me6.[fm_pos_loctype] = 'YARD'))) AS dual_from_pos_gkey
------------------------
			  ,dual.[gkey] AS dual_trans_gkey
			  ,dual.[nbr] AS dual_trans_nbr
			  ,dual.[ctr_ticket_pos_id] AS dual_ctr_ticket_pos_id
			  ,irl.[id] AS entry_lane
			  ,orl.[id] AS exit_lane
			  ,DATEADD(HOUR, -21, DATEADD(DAY, DATEDIFF(DAY, 0, td.[entered_yard]), 0)) AS truck_entered_prior_day
			  ,DATEADD(HOUR, 3, DATEADD(DAY, DATEDIFF(DAY, 0, td.[entered_yard]), 0)) AS truck_entered_day
			  ,DATEADD(DAY, DATEDIFF(DAY, 0, td.[entered_yard]), 0) AS trunc_truck_entered
			  ,IIF(CHARINDEX('.', tt.[ctr_pos_slot]) = 0
				  ,tt.[ctr_pos_slot]
				  ,SUBSTRING(tt.[ctr_pos_slot], 1, CHARINDEX('.', tt.[ctr_pos_slot]) - 1)) AS ctr_pos_id
		      ,tt.[ctr_ticket_pos_id]
			  ,tt.[ctr_gross_weight]
			  ,tt.[eqo_nbr]
			  ,tt.[unit_gkey]
			  ,tt.[appointment_nbr]
		FROM   [SparcsN4R].[dbo].[road_truck_transactions]          AS tt   WITH (NOLOCK)
		JOIN   [SparcsN4R].[dbo].[road_truck_visit_details]         AS td   WITH (NOLOCK) ON td.[tvdtls_gkey] = tt.[truck_visit_gkey]
		JOIN   [SparcsN4R].[dbo].[ref_bizunit_scoped]               AS tc   WITH (NOLOCK) ON tc.[id] = tt.[trkco_id] AND
				                                                                             tc.[role] = 'HAULIER'
		LEFT   JOIN [SparcsN4R].[dbo].[inv_unit]                    AS u    WITH (NOLOCK) ON u.[gkey] = tt.[unit_gkey]
		OUTER APPLY (SELECT TOP 1 uv.[gkey], uv.[time_in], uv.[arrive_pos_loctype], uv.[actual_ib_cv] 
					FROM [SparcsN4R].[dbo].[inv_unit_fcy_visit]          AS uv   WITH (NOLOCK) WHERE uv.[unit_gkey] = tt.[unit_gkey] AND
		                                                                                     uv.[time_in]  IS NOT NULL AND
                                                                                             (
                                                                                              ((CONVERT(DATE,uv.[time_move]) <= CONVERT(DATE,td.[exited_yard]) OR 
																							    td.[exited_yard] IS NULL)
                                                                                                   AND 
																							   CONVERT(DATE,uv.[time_move]) >= CONVERT(DATE,td.[entered_yard]))
                                                                                              OR
                                                                                              ((CONVERT(DATE,uv.[time_in]) <= CONVERT(date,td.[exited_yard]) OR 
																							   td.[exited_yard] IS NULL)
                                                                                                   AND 
																							   CONVERT(DATE,uv.[time_in]) >= CONVERT(date,td.[entered_yard]))
                                                                                             )
					ORDER BY uv.[gkey]) as uv
		LEFT   JOIN [SparcsN4R].[dbo].[ref_equipment]               AS e    WITH (NOLOCK) ON e.[gkey] = u.[eq_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[ref_equip_type]              AS et   WITH (NOLOCK) ON et.[gkey] = e.[eqtyp_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[argo_carrier_visit]          AS icv  WITH (NOLOCK) ON icv.[gkey] = uv.[actual_ib_cv]
		LEFT   JOIN [SparcsN4R].[dbo].[vsl_vessel_visit_details]    AS vvd  WITH (NOLOCK) ON vvd.[vvd_gkey] = icv.[cvcvd_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[vsl_vessels]                 AS vsl  WITH (NOLOCK) ON vsl.[gkey] = vvd.[vessel_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[ref_routing_point]           AS pol  WITH (NOLOCK) ON pol.[gkey] = tt.[pol_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[ref_routing_point]           AS pod  WITH (NOLOCK) ON pod.[gkey] = tt.[pod1_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[argo_lanes]                  AS irl  WITH (NOLOCK) ON irl.[gkey] = td.[entry_gateln_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[argo_lanes]                  AS orl  WITH (NOLOCK) ON orl.[gkey] = td.[exit_gateln_gkey]
		OUTER  APPLY 
		(
			SELECT TOP 1 tt4.[sub_type]
			            ,tt4.[line_id]
						,tt4.[gkey]
						,tt4.[nbr]
						,tt4.[temp_required]
						,tt4.[ctr_freight_kind]
						,tt4.[unit_gkey]
						,tt4.[ctr_ticket_pos_id]
			FROM   [SparcsN4R].[dbo].[road_truck_transactions]   AS tt4 WITH (NOLOCK)
			WHERE  tt4.[status] = tt.[status]
			AND    LEFT(tt4.[sub_type], 1) <> LEFT(tt.[sub_type], 1)
			AND    tt4.[truck_visit_gkey] = tt.[truck_visit_gkey]
			ORDER BY tt4.[truck_visit_gkey] DESC
		) AS dual
		--OUTER  APPLY (
		--	SELECT TOP 1 me2.[fm_pos_bin] 
		--	FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me2 WITH (NOLOCK)
		--	WHERE  me2.[ufv_gkey] = uv.[gkey]
		--	AND    me2.[fm_pos_loctype] = 'YARD'
		--	ORDER BY me2.[mve_gkey] DESC 
		--) memax
		--OUTER  APPLY (
		--	SELECT TOP 1 me2.[to_pos_bin] 
		--	FROM   [SparcsN4R].[dbo].[inv_move_event]  AS me2 WITH (NOLOCK)
		--	WHERE  me2.[ufv_gkey] = uv.[gkey]
		--	AND    me2.[fm_pos_loctype] = 'YARD'
		--	ORDER BY me2.[mve_gkey]
		--) memin

		--OUTER  APPLY (
		--	SELECT TOP 1 me4.[fm_pos_bin] 
		--	FROM   [SparcsN4R].[dbo].[inv_move_event]            AS me4 WITH (NOLOCK)
		--	JOIN   [SparcsN4R].[dbo].[inv_unit_fcy_visit]        AS uv2 WITH (NOLOCK) ON uv2.[gkey] = me4.[ufv_gkey] AND dual.[unit_gkey] = uv2.[unit_gkey]
		--	WHERE  me4.[fm_pos_loctype] = 'YARD'
		--	ORDER BY me4.[mve_gkey] DESC 
		--) dualmemax
		--OUTER  APPLY (
		--	SELECT TOP 1 me4.[to_pos_bin] 
		--	FROM   [SparcsN4R].[dbo].[inv_move_event]            AS me4 WITH (NOLOCK)
		--	JOIN   [SparcsN4R].[dbo].[inv_unit_fcy_visit]        AS uv2 WITH (NOLOCK) ON uv2.[gkey] = me4.[ufv_gkey] AND dual.[unit_gkey] = uv2.[unit_gkey]
		--	WHERE  me4.[fm_pos_loctype] = 'YARD'
		--	ORDER BY me4.[mve_gkey]
		--) dualmemin
		WHERE  tt.[status] = 'COMPLETE'
		UNION ALL
        SELECT tt.[original_gkey] AS gkey
			  ,tt.[nbr] AS transaction_nbr
			  ,ISNULL(tt.[ctr_id], u.[id]) AS eq_nbr
			  ,SUBSTRING(et.[nominal_length], 4, LEN(et.[nominal_length]) - 3) AS eqsz_id
			  ,et.[iso_group] AS eqtp_id
			  ,SUBSTRING(et.[nominal_height], 4, LEN(et.[nominal_height]) - 3) AS eqht_id
			  ,td.[entered_yard] AS truck_entered
			  ,td.[exited_yard] AS truck_exited
			  ,ISNULL(uv.[time_in], td.[entered_yard]) AS in_time
			  ,tt.[chs_id] AS chs_nbr
			  ,td.[truck_license_nbr]
			  ,CONCAT(FORMAT(td.[entered_yard], 'yyyyMMddHHmmss'), td.[truck_license_nbr]) AS truck_entered_lic
			  ,CASE -- LAX 2nd shift for the day ends at 3 AM of the following day
				   WHEN (SELECT [DEFAULT_VAL]
						 FROM   [dbo].[TOS_PARAMETER]
						 WHERE  [PARAMETER_CD] = 'THISTERM') = 'LAX' AND 
						DATEPART(HOUR, td.[entered_yard]) < 3 THEN 
					   FORMAT(DATEADD(DAY, -1, td.[entered_yard]), 'yyyyMMdd')
				   ELSE
					   FORMAT(td.[entered_yard], 'yyyyMMdd') 
			   END AS date_id
		      ,tt.[line_id]
			  ,tt.[sub_type] AS trans_type
			  ,CASE tt.[sub_type]
				   WHEN 'RM' THEN 'Receive Empty'
				   WHEN 'RE' THEN 'Receive Export'
				   WHEN 'RI' THEN 'Receive Import'
				   WHEN 'RC' THEN 'Receive Chassis'
				   WHEN 'DM' THEN 'Deliver Empty'
				   WHEN 'DE' THEN 'Deliver Export'
				   WHEN 'DI' THEN 'Deliver Import'
				   WHEN 'DC' THEN 'Deliver Chassis'
				   ELSE tt.[sub_type]
			   END AS trans_type_text
			  ,tt.[trkco_id] AS trucker_id
			  ,tc.[name] AS trucker_name
			  ,(SELECT [DEFAULT_VAL]
			    FROM   [dbo].[TOS_PARAMETER]
				WHERE  [PARAMETER_CD] = 'THISTERM') AS location_code
			  ,IIF(uv.[arrive_pos_loctype] = 'VESSEL', vsl.[id], NULL) AS ship_id
			  ,IIF(uv.[arrive_pos_loctype] = 'VESSEL', vvd.[ib_vyg], NULL) AS voy_nbr
			  ,IIF(uv.[arrive_pos_loctype] = 'VESSEL', icv.[ata], NULL) AS voy_ata
			  --,ISNULL((SELECT me1.[fm_pos_bin] 
					--   FROM   [SparcsN4R].[dbo].[inv_ar_move_event]  AS me1 WITH (NOLOCK)
					--   WHERE  me1.[arMve_gkey] = (SELECT MAX(me2.[arMve_gkey])
					--							  FROM   [SparcsN4R].[dbo].[inv_ar_move_event]  AS me2 WITH (NOLOCK)
				 --       						  WHERE  me2.[ufv_gkey] = uv.[original_gkey]
					--							  AND    me2.[fm_pos_loctype] = 'YARD'))
					-- ,(SELECT me3.[to_pos_bin]
					--	FROM   [SparcsN4R].[dbo].[inv_ar_move_event]  AS me3 WITH (NOLOCK)
					--	WHERE  me3.[arMve_gkey] = (SELECT MIN(me4.[arMve_gkey])
					--							   FROM   [SparcsN4R].[dbo].[inv_ar_move_event]  AS me4 WITH (NOLOCK)
					--							   WHERE  me4.[ufv_gkey] = uv.[original_gkey]
					--							   AND    me4.[to_pos_loctype] = 'YARD'))) AS from_pos_gkey
			  ,tt.[group_id]
			  ,IIF(ISNULL(tt.[had_trouble], 0) = 1, 'Y', 'N') AS trouble_fl
			  ,IIF(ISNULL(tt.[is_hazard], 0) = 1, 'Y', 'N') AS haz_fl
			  ,IIF(ISNULL(tt.[is_oog], 0) = 1, 'Y', 'N') AS oog_fl
			  ,IIF(tt.[temp_required] IS NOT NULL OR 
				   (tt.[ctr_freight_kind] = 'MTY' AND et.[iso_group] = 'RE'), 'Y', 'N') AS reefer_fl
			  ,tt.[pol_id] AS pod
			  ,tt.[pod1_id] AS pol
			  ,ISNULL((SELECT MIN(rd1.[created])
				       FROM   [SparcsN4R].[dbo].[road_ar_documents]   AS rd1 WITH (NOLOCK)
				       WHERE  rd1.[tran_original_gkey] = (SELECT MIN(tt2.[original_gkey])
												          FROM  [SparcsN4R].[dbo].[road_ar_truck_transactions]  AS tt2 WITH (NOLOCK)
												          JOIN  [SparcsN4R].[dbo].[road_ar_truck_visit_details] AS td2 WITH (NOLOCK) ON td2.[arTvdtls_gkey] = tt2.[truck_visit_gkey] AND
																											                            td2.[truck_license_nbr] = td.[truck_license_nbr] AND
																											                            td2.[entered_yard] = td.[entered_yard]
											              WHERE tt2.[status] = tt.[status]))
					  ,td.[entered_yard]) AS start_time
			  ,CASE
				   WHEN tt.[sub_type] LIKE 'D%' THEN 
					   (SELECT MAX(rd2.[created])
						FROM  [SparcsN4R].[dbo].[road_ar_documents]  AS rd2 WITH (NOLOCK)
						WHERE  rd2.[tran_original_gkey] = tt.[original_gkey])
				   ELSE ISNULL((SELECT MAX(rd3.[created])
								FROM   [SparcsN4R].[dbo].[road_ar_documents]           AS rd3 WITH (NOLOCK)
								JOIN   [SparcsN4R].[dbo].[road_ar_truck_transactions]  AS tt3 WITH (NOLOCK) ON tt3.[original_gkey] = rd3.[tran_original_gkey] AND
																								               tt3.[status] = tt.[status] AND
																								               LEFT(tt3.[sub_type], 1) <> LEFT(tt.[sub_type], 1)
								JOIN   [SparcsN4R].[dbo].[road_ar_truck_visit_details] AS td3 WITH (NOLOCK) ON td3.[arTvdtls_gkey] = tt3.[truck_visit_gkey] AND
																								               td3.[entered_yard] = td.[entered_yard] AND
																								               td3.[truck_license_nbr] = td.[truck_license_nbr])
							  ,(SELECT MAX(rd4.[created])
								FROM   [SparcsN4R].[dbo].[road_ar_documents]   AS rd4 WITH (NOLOCK)
								WHERE  rd4.[tran_original_gkey] = tt.[original_gkey]))
			   END AS end_time
			  ,(SELECT MIN(rd6.[created])
				FROM   [SparcsN4R].[dbo].[road_ar_documents]      AS rd6 WITH (NOLOCK)
				WHERE  rd6.[tran_original_gkey] = tt.[original_gkey]
				AND    rd6.[doc_type_id] = 'ERROR' AND tt.[had_trouble] = 1) AS trouble_start

			  ,ISNULL(dual.[sub_type],'N') AS dual_type
			  ,ISNULL(dual.[line_id],'N') AS dual_line
			  ,ISNULL((SELECT IIF(dual.[temp_required] IS NOT NULL OR
									  (dual.[ctr_freight_kind] = 'MTY' AND et2.[iso_group] = 'RE'), 'Y', 'N')
					   FROM   [SparcsN4R].[dbo].[inv_ar_unit]                  AS u2  WITH (NOLOCK)
					   JOIN   [SparcsN4R].[dbo].[ref_equipment]                AS eq2 WITH (NOLOCK) ON eq2.[id_full] = u2.[id] AND
							                                                                           eq2.[scope] = 2
					   JOIN   [SparcsN4R].[dbo].[ref_equip_type]               AS et2 WITH (NOLOCK) ON et2.[gkey] = eq2.[eqtyp_gkey]
					   WHERE  u2.[original_gkey] = dual.[ctrunit_gkey])
						,'N') AS dual_reefer
			  --,ISNULL((SELECT me3.[fm_pos_bin]
					--   FROM   [SparcsN4R].[dbo].[inv_ar_move_event]  AS me3 WITH (NOLOCK)
					--   WHERE  me3.[arMve_gkey] = (SELECT MAX(me4.[arMve_gkey])
					--							  FROM   [SparcsN4R].[dbo].[inv_ar_move_event]            AS me4 WITH (NOLOCK)
					--							  JOIN   [SparcsN4R].[dbo].[inv_ar_unit_fcy_visit]        AS uv2 WITH (NOLOCK) ON uv2.[original_gkey] = me4.[ufv_gkey] AND dual.[ctrunit_gkey] = uv2.[unit_gkey]
					--							  WHERE  me4.[fm_pos_loctype] = 'YARD'))
					-- ,(SELECT me5.[to_pos_bin]
					--   FROM   [SparcsN4R].[dbo].[inv_ar_move_event]  AS me5 WITH (NOLOCK)
					--   WHERE  me5.[arMve_gkey] = (SELECT MAX(me6.[arMve_gkey])
					--							  FROM   [SparcsN4R].[dbo].[inv_ar_move_event]            AS me6 WITH (NOLOCK)
					--							  JOIN   [SparcsN4R].[dbo].[inv_ar_unit_fcy_visit]        AS uv3 WITH (NOLOCK) ON uv3.[original_gkey] = me6.[ufv_gkey] AND dual.[ctrunit_gkey] = uv3.[unit_gkey]
					--							  WHERE  me6.[fm_pos_loctype] = 'YARD'))) AS dual_from_pos_gkey
			  ,dual.[original_gkey] AS dual_trans_gkey
			  ,dual.[nbr] AS dual_trans_nbr
			  ,dual.[ctr_ticket_pos_id] AS dual_ctr_ticket_pos_id
			  ,td.[entry_lane_id] AS entry_lane
			  ,td.[exit_lane_id] AS exit_lane
			  ,DATEADD(HOUR, -21, DATEADD(DAY, DATEDIFF(DAY, 0, td.[entered_yard]), 0)) AS truck_entered_prior_day
			  ,DATEADD(HOUR, 3, DATEADD(DAY, DATEDIFF(DAY, 0, td.[entered_yard]), 0)) AS truck_entered_day
			  ,DATEADD(DAY, DATEDIFF(DAY, 0, td.[entered_yard]), 0) AS trunc_truck_entered
			  ,IIF(CHARINDEX('.', tt.[ctr_pos_slot]) = 0
			  ,tt.[ctr_pos_slot]
			  ,SUBSTRING(tt.[ctr_pos_slot], 1, CHARINDEX('.', tt.[ctr_pos_slot]) - 1)) AS ctr_pos_id
			  ,tt.[ctr_ticket_pos_id]
			  ,tt.[ctr_gross_weight]
			  ,tt.[eqo_nbr]
			  ,u.[original_gkey] AS unit_gkey
			  ,tt.[appointment_nbr]
		FROM   [SparcsN4R].[dbo].[road_ar_truck_transactions]       AS tt   WITH (NOLOCK)
		JOIN   [SparcsN4R].[dbo].[road_ar_truck_visit_details]      AS td   WITH (NOLOCK) ON td.[arTvdtls_gkey] = tt.[truck_visit_gkey]
		JOIN   [SparcsN4R].[dbo].[ref_bizunit_scoped]               AS tc   WITH (NOLOCK) ON tc.[id] = tt.[trkco_id] AND
				                                                                             tc.[role] = 'HAULIER'
		LEFT   JOIN [SparcsN4R].[dbo].[inv_ar_unit]                 AS u    WITH (NOLOCK) ON u.[original_gkey] = tt.[ctrunit_gkey]
		OUTER APPLY (SELECT TOP 1 uv.[original_gkey], uv.[time_in], uv.[arrive_pos_loctype], uv.[actual_ib_cv_gkey] 
					FROM [SparcsN4R].[dbo].[inv_ar_unit_fcy_visit]       AS uv   WITH (NOLOCK) WHERE uv.[unit_gkey] = u.[original_gkey] AND
		                                                                                     uv.[time_in]  IS NOT NULL 
																							 AND
                                                                                             (
                                                                                              ((CONVERT(DATE,uv.[time_move]) <= CONVERT(DATE,td.[exited_yard]) OR 
																							    td.[exited_yard] IS NULL)
                                                                                                   AND 
																							   CONVERT(DATE,uv.[time_move]) >= CONVERT(DATE,td.[entered_yard]))
                                                                                              OR
                                                                                              ((CONVERT(DATE,uv.[time_in]) <= CONVERT(date,td.[exited_yard]) OR 
																							   td.[exited_yard] IS NULL)
                                                                                                   AND 
																							   CONVERT(DATE,uv.[time_in]) >= CONVERT(date,td.[entered_yard]))
                                                                                             )
						ORDER BY uv.[gkey]) as uv
		LEFT   JOIN [SparcsN4R].[dbo].[ref_equipment]               AS e    WITH (NOLOCK) ON e.[id_full] = ISNULL(tt.[ctr_id], u.[id]) AND
				                                                                             e.[scope] = 2
		LEFT   JOIN [SparcsN4R].[dbo].[ref_equip_type]              AS et   WITH (NOLOCK) ON et.[gkey] = e.[eqtyp_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[argo_ar_carrier_visit]       AS icv  WITH (NOLOCK) ON icv.[original_gkey] = uv.[actual_ib_cv_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[argo_ar_visit_details]       AS ivd  WITH (NOLOCK) ON ivd.[cv_gkey] = icv.[original_gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[vsl_ar_vessel_visit_details] AS vvd  WITH (NOLOCK) ON vvd.[arVvd_gkey] = ivd.[gkey]
		LEFT   JOIN [SparcsN4R].[dbo].[vsl_vessels]                 AS vsl  WITH (NOLOCK) ON vsl.[gkey] = vvd.[vessel_gkey]

		OUTER  APPLY 
		(
			SELECT TOP 1 tt4.[sub_type]
			            ,tt4.[line_id]
						,tt4.[original_gkey]
						,tt4.[nbr]
						,tt4.[temp_required]
						,tt4.[ctr_freight_kind]
						,tt4.[ctrunit_gkey]
						,tt4.[ctr_ticket_pos_id]
			FROM   [SparcsN4R].[dbo].[road_ar_truck_transactions]   AS tt4 WITH (NOLOCK)
			WHERE  tt4.[status] = tt.[status]
			AND    LEFT(tt4.[sub_type], 1) <> LEFT(tt.[sub_type], 1)
			AND    tt4.[truck_visit_gkey] = tt.[truck_visit_gkey]
			ORDER BY tt4.[truck_visit_gkey] DESC
		) AS dual

		WHERE  tt.[status] = 'COMPLETE'
)  AS a
;



GO


