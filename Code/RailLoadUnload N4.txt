USE [TOSGateway]
GO

/****** Object:  StoredProcedure [dbo].[usp_RPT_GetRailLoadUnloadTest]    Script Date: 6/20/2019 2:43:15 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


-- =============================================
-- Author:		T. Reilly, Advent Intermodal Solutions
-- Create date: 2/13/2019
-- Description:	Procedure used for the LSA Rail Load-Unload Detail - LSAN4 report
-- =============================================
CREATE PROCEDURE [dbo].[usp_RPT_GetRailLoadUnload]
	@Performed_Start_Dt    DATETIME, 
	@Performed_End_Dt      DATETIME,
	@Line                  NVARCHAR(MAX) = 'ALL'
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;

	DECLARE @dRunStartDt  DATETIME = @Performed_Start_Dt;
	DECLARE @dRunEndDt    DATETIME = @Performed_End_Dt
	DECLARE @tLines       TABLE 
	(
	[item]    NVARCHAR(100)
	);

	INSERT
	INTO   @tLines
	SELECT [Item]
	FROM   [dbo].[TFN_ParseArray](@Line, ',');

	WITH q_vessel_visits AS (SELECT cv.[gkey],
									vvd.[vvd_gkey],
									vvd.[ib_vyg],
									vvd.[ob_vyg],
									vsl.[id] AS [vessel_id],
									svc.[id] AS [vessel_service],
									vl.[id] AS [vessel_line],
									vvl.[begin_receive] AS [vessel_1st_receiving],
									vvl.[cargo_cutoff] AS [vessel_cutoff]
							 FROM   [SparcsN4R].[dbo].[argo_carrier_visit]        AS cv  (NOLOCK)
							 JOIN   [SparcsN4R].[dbo].[vsl_vessel_visit_details]  AS vvd (NOLOCK) ON vvd.[vvd_gkey] = cv.[cvcvd_gkey]
							 JOIN   [SparcsN4R].[dbo].[argo_visit_details]        AS vd  (NOLOCK) ON vd.[gkey] = vvd.[vvd_gkey]
							 JOIN   [SparcsN4R].[dbo].[vsl_vessels]               AS vsl (NOLOCK) ON vsl.[gkey] = vvd.[vessel_gkey]
							 JOIN   [SparcsN4R].[dbo].[ref_bizunit_scoped]        AS vl  (NOLOCK) ON vl.[gkey] = vvd.[bizu_gkey] AND
																									 vl.[role] = 'LINEOP'
							 JOIN   [SparcsN4R].[dbo].[vsl_vessel_visit_lines]    AS vvl (NOLOCK) ON vvl.[vvd_gkey] = vvd.[vvd_gkey] AND
																									 vvl.[line_gkey] = vl.[gkey]
							 LEFT   JOIN [SparcsN4R].[dbo].[ref_carrier_service]  AS svc (NOLOCK) ON svc.[gkey] = vd.[service]
							 WHERE  cv.[carrier_mode] = 'VESSEL'
							 UNION ALL
							 SELECT acv.[original_gkey]  AS [gkey],
									avvd.[arvvd_gkey]    AS [vvd_gkey],
									avvd.[ib_vyg],
									avvd.[ob_vyg],
									avsl.[id] AS [vessel_id],
									asvc.[id] AS [vessel_service],
									avvd.[bizu_id] AS [vessel_line],
									avvl.[begin_receive] AS [vessel_1st_receiving],
									avvl.[cargo_cutoff] AS [vessel_cutoff]
							 FROM   [SparcsN4R].[dbo].[argo_ar_carrier_visit]       AS acv  (NOLOCK)
							 JOIN   [SparcsN4R].[dbo].[argo_ar_visit_details]       AS avd  (NOLOCK) ON avd.[cv_gkey] = acv.[original_gkey]
							 JOIN   [SparcsN4R].[dbo].[vsl_ar_vessel_visit_details] AS avvd (NOLOCK) ON avvd.[arVVD_gkey] = avd.[gkey]
							 JOIN   [SparcsN4R].[dbo].[vsl_vessels]                 AS avsl (NOLOCK) ON avsl.[gkey] = avvd.[vessel_gkey]
							 JOIN   [SparcsN4R].[dbo].[vsl_ar_vessel_visit_lines]   AS avvl (NOLOCK) ON avvl.[vvd_gkey] = avvd.[arVVD_gkey] AND
																										avvl.[line_id] = avvd.[bizu_id]
							 LEFT   JOIN [SparcsN4R].[dbo].[ref_carrier_service]    AS asvc (NOLOCK) ON asvc.[gkey] = avd.[service_gkey]
							 WHERE  acv.[carrier_mode] = 'VESSEL'),
		 q_rail_visits AS (SELECT cv.[gkey],
		                          cv.[id],
								  rvd.[gkey] AS [rvd_gkey],
								  vl.[id] AS [railroad_line]
						   FROM   [SparcsN4R].[dbo].[argo_carrier_visit]           AS cv  (NOLOCK)
						   JOIN   [SparcsN4R].[dbo].[rail_train_visit_details]     AS rvd (NOLOCK) ON rvd.[gkey] = cv.[cvcvd_gkey]
						   JOIN   [SparcsN4R].[dbo].[ref_bizunit_scoped]           AS vl  (NOLOCK) ON vl.[gkey] = rvd.[rr_gkey]
						   WHERE  cv.[carrier_mode] IN ('TRAIN', 'RAILCAR')
						   UNION ALL
						   SELECT acv.[original_gkey] AS [gkey],
						          acv.[id],
								  arvd.[arGkey] AS [rvd_gkey],
								  arvd.[rr_id] AS [railroad_line]
						   FROM   [SparcsN4R].[dbo].[argo_ar_carrier_visit]        AS acv  (NOLOCK)
						   JOIN   [SparcsN4R].[dbo].[argo_ar_visit_details]        AS avd  (NOLOCK) ON avd.[cv_gkey] = acv.[original_gkey]
						   JOIN   [SparcsN4R].[dbo].[rail_ar_train_visit_details]  AS arvd (NOLOCK) ON arvd.[arGkey] = avd.[gkey]
						   WHERE  [carrier_mode] IN ('TRAIN', 'RAILCAR'))
	SELECT u.[gkey] AS unit_gkey,
		   se1.[gkey] AS service_event_gkey,
		   u.[id] eq_nbr,
 		   SUBSTRING(et.[nominal_length], 4, LEN(et.[nominal_length]) - 3) AS eqsz_id,
		   et.[iso_group] AS eqtp_id,
		   SUBSTRING(et.[nominal_height], 4, LEN(et.[nominal_height]) - 3) AS eqht_id,
		   u.[goods_and_ctr_wt_kg] AS gross_weight,
		   IIF(u.[freight_kind] = 'MTY', 'E', 'F') AS status,
		   CASE u.[category]
			   WHEN 'STRGE' THEN 'M'
			   WHEN 'IMPRT' THEN 'I'
			   WHEN 'EXPRT' THEN 'E'
			   WHEN 'TRSHP' THEN 'T'                                                            
			   WHEN 'THRGH' THEN 'R'
			   ELSE u.[category]
		   END AS category,
		   ln.[id] AS line_id,
		   CASE 
			   WHEN set1.[id] = 'UNIT_RAMP' THEN 
				   'Rail Load'
			   WHEN set1.[id] = 'UNIT_DERAMP' THEN 
				   'Rail Unload'
		   END AS tserv_id,
		   se1.[placed_time] AS performed, 	
		   se1.[placed_by] AS move_recorded_by,
		   uv.[time_in] AS yard_in_time,
		   uv.[time_out] AS yard_out_time,
		   uv.[last_pos_loctype],
		   uv.[last_pos_locid],
		   mv.[fm_pos_loctype],
		   mv.[fm_pos_locid],
		   mv.[to_pos_loctype],
		   mv.[to_pos_locid],
		   ROUND(DATEDIFF(HOUR, uv.[time_in], uv.[time_out]) / 24.0, 2) AS dwell,
		   pt.[id] AS pod,	   
		   CASE
			   WHEN ISNULL(uv.[arrive_pos_loctype], mv.[fm_pos_loctype]) = 'VESSEL' THEN
				   aivv.[vessel_service]
			   WHEN ISNULL(mv.[to_pos_loctype], uv.[last_pos_loctype]) = 'VESSEL' THEN
				   aovv.[vessel_service]
			   ELSE
				   NULL
		   END AS vessel_service,
		   CASE ISNULL(uv.[arrive_pos_loctype], mv.[fm_pos_loctype]) 
			   WHEN 'VESSEL' THEN 
				   aivv.[vessel_id] + aivv.[ib_vyg] 
			   ELSE
				   uv.[arrive_pos_locid]
		   END AS arrive_carrier,
		   CASE ISNULL(mv.[to_pos_loctype], uv.[last_pos_loctype])
			   WHEN 'VESSEL' THEN
				   aovv.[vessel_id] + aovv.[ob_vyg]
			   ELSE
				   uv.[last_pos_locid]
		   END AS depart_carrier,
		   IIF(gd.[temp_reqd_C] IS NULL, 'No', 'Yes') AS reefer_flg,
		   airv.[id] AS arrive_train_id,
		   ISNULL(aorv.[id], iorv.[id]) AS depart_train_id,
		   sva.[placed_time] AS rail_arrive_time,	 
		   svd.[placed_time] AS rail_depart_time,
		   CASE set1.[id]
			   WHEN 'UNIT_RAMP' THEN 
				   ISNULL(aorv.[railroad_line], iorv.[railroad_line])
			   WHEN 'UNIT_DERAMP' THEN
				   airv.[railroad_line]
		   END AS railroad_line,
		   uv.[arrive_pos_slot] AS arrival_position,
		   uv.[last_pos_slot] AS discharge_spot,
		   eo.[nbr] AS booking,
		   [dbo].[TFN_GetBillOfLading](u.[gkey]) AS bill_of_lading,
		   ISNULL(aovv.[vessel_cutoff], iovv.[vessel_cutoff]) AS vessel_cutoff,
		   ISNULL(aovv.[vessel_1st_receiving], iovv.[vessel_1st_receiving]) AS vessel_1st_receiving,
		   mv.[pow]
	FROM   [SparcsN4R].[dbo].[inv_unit]                      AS u     (NOLOCK)
	JOIN   [SparcsN4R].[dbo].[inv_unit_fcy_visit]            AS uv    (NOLOCK) ON uv.[unit_gkey] = u.[gkey]
	JOIN   [SparcsN4R].[dbo].[srv_event]                     AS se1   (NOLOCK) ON se1.[applied_to_class] = 'UNIT' AND
																				  se1.[placed_time] BETWEEN @dRunStartDt 
																									AND     @dRunEndDt AND
																				  se1.[applied_to_gkey] = u.[gkey] 
	JOIN   [SparcsN4R].[dbo].[srv_event_types]               AS set1  (NOLOCK) ON set1.[gkey] = se1.[event_type_gkey] AND
																				  set1.[id] IN ('UNIT_RAMP', 'UNIT_DERAMP')
	JOIN   [SparcsN4R].[dbo].[inv_move_event]                AS mv    (NOLOCK) ON mv.[ufv_gkey] = uv.[gkey] AND
																				  (mv.[fm_pos_loctype] IN ('TRAIN', 'RAILCAR') OR
																				   mv.[to_pos_loctype] IN ('TRAIN', 'RAILCAR')) AND
																				  mv.[mve_gkey] = se1.[gkey]
	JOIN   [SparcsN4R].[dbo].[ref_equipment]                 AS e     (NOLOCK) ON e.[gkey] = u.[eq_gkey]
	JOIN   [SparcsN4R].[dbo].[ref_equip_type]                AS et    (NOLOCK) ON et.[gkey] = e.[eqtyp_gkey]
	JOIN   [SparcsN4R].[dbo].[ref_bizunit_scoped]            AS ln    (NOLOCK) ON ln.[gkey] = u.[line_op] AND
																				  (ln.[id] IN (SELECT [item] FROM @tLines) OR
															     				   'ALL' IN (SELECT [item] FROM @tLines))
	LEFT   JOIN [SparcsN4R].[dbo].[ref_routing_point]        AS pt    (NOLOCK) ON pt.[gkey] = u.[pod1_gkey]
	LEFT   JOIN [SparcsN4R].[dbo].[inv_goods]                AS gd    (NOLOCK) ON gd.[gkey] = u.[goods]
	LEFT   JOIN [SparcsN4R].[dbo].[inv_eq_base_order_item]   AS eoi   (NOLOCK) ON eoi.[gkey] = u.[depart_order_item_gkey]
	LEFT   JOIN [SparcsN4R].[dbo].[inv_eq_base_order]        AS eo    (NOLOCK) ON eo.[gkey] = eoi.[eqo_gkey]
	LEFT   JOIN q_vessel_visits                              AS aivv           ON aivv.[gkey] = uv.[actual_ib_cv]
	LEFT   JOIN q_vessel_visits                              AS iovv           ON iovv.[gkey] = uv.[intend_ob_cv]
	LEFT   JOIN q_vessel_visits                              AS aovv           ON aovv.[gkey] = uv.[actual_ob_cv]
	LEFT   JOIN q_rail_visits                                AS airv           ON airv.[gkey] = uv.[actual_ib_cv]
	LEFT   JOIN q_rail_visits                                AS iorv           ON iorv.[gkey] = uv.[intend_ob_cv]
	LEFT   JOIN q_rail_visits                                AS aorv           ON aorv.[gkey] = uv.[actual_ob_cv]
	OUTER  APPLY (SELECT TOP 1 q1.[placed_time]
				  FROM   (SELECT se3.[placed_time]
						  FROM   [SparcsN4R].[dbo].[srv_event]           AS se3  (NOLOCK)
						  JOIN   [SparcsN4R].[dbo].[srv_event_types]     AS set3 (NOLOCK) ON set3.[gkey] = se3.[event_type_gkey] AND
																							 set3.[id] = 'UNIT_OUT_RAIL'
						  WHERE  se3.[applied_to_class] = se1.[applied_to_class]
						  AND    se3.[placed_time] BETWEEN @dRunStartDt
												   AND     @dRunEndDt
						  AND    se3.[applied_to_gkey] = u.[gkey]
						  UNION ALL
						  SELECT ase3.[placed_time]
						  FROM   [SparcsN4R].[dbo].[inv_ar_unit]         AS au3  (NOLOCK)
						  JOIN   [SparcsN4R].[dbo].[srv_ar_event]        AS ase3 (NOLOCK) ON ase3.[applied_to_class] = se1.[applied_to_class] AND
																							 ase3.[placed_time] BETWEEN @dRunStartDt AND
																														@dRunEndDt AND
																							 ase3.[applied_to_gkey] = au3.[original_gkey] AND
																							 ase3.[event_type_id] = 'UNIT_OUT_RAIL'
						  WHERE  au3.[original_gkey] = u.[gkey]) AS q1
				  ORDER  BY q1.[placed_time] DESC) AS svd
	OUTER  APPLY (SELECT TOP 1 q2.[placed_time]
				  FROM   (SELECT se4.[placed_time]
						  FROM   [SparcsN4R].[dbo].[srv_event]           AS se4  (NOLOCK)
						  JOIN   [SparcsN4R].[dbo].[srv_event_types]     AS set4 (NOLOCK) ON set4.[gkey] = se4.[event_type_gkey] AND
																							 set4.[id] = 'UNIT_IN_RAIL'
						  WHERE  se4.[applied_to_class] = se1.[applied_to_class]
						  AND    se4.[placed_time] BETWEEN @dRunStartDt
												   AND     @dRunEndDt
						  AND    se4.[applied_to_gkey] = u.[gkey]
						  UNION ALL
						  SELECT ase4.[placed_time]
						  FROM   [SparcsN4R].[dbo].[inv_ar_unit]         AS au4  (NOLOCK)
						  JOIN   [SparcsN4R].[dbo].[srv_ar_event]        AS ase4 (NOLOCK) ON ase4.[applied_to_class] = se1.[applied_to_class] AND
																							 ase4.[placed_time] BETWEEN @dRunStartDt AND
																														@dRunEndDt AND
																							 ase4.[applied_to_gkey] = au4.[original_gkey] AND
																							 ase4.[event_type_id] = 'UNIT_IN_RAIL'
						  WHERE  au4.[original_gkey] = u.[gkey]) AS q2
				  ORDER  BY q2.[placed_time] DESC) AS sva
	UNION ALL
	SELECT u.[original_gkey] AS unit_gkey,
		   se1.[original_gkey] AS service_event_gkey,
		   u.[id] eq_nbr,
 		   SUBSTRING(et.[nominal_length], 4, LEN(et.[nominal_length]) - 3) AS eqsz_id,
		   et.[iso_group] AS eqtp_id,
		   SUBSTRING(et.[nominal_height], 4, LEN(et.[nominal_height]) - 3) AS eqht_id,
		   u.[goods_and_ctr_wt_kg] AS gross_weight,
		   IIF(u.[freight_kind] = 'MTY', 'E', 'F') AS status,
		   CASE u.[category]
			   WHEN 'STRGE' THEN 'M'
			   WHEN 'IMPRT' THEN 'I'
			   WHEN 'EXPRT' THEN 'E'
			   WHEN 'TRSHP' THEN 'T'                                                            
			   WHEN 'THRGH' THEN 'R'
			   ELSE u.[category]
		   END AS category,
		   u.[line_op_id] AS line_id,
		   CASE 
			   WHEN se1.[event_type_id] = 'UNIT_RAMP' THEN 
				   'Rail Load'
			   WHEN se1.[event_type_id] = 'UNIT_DERAMP' THEN 
				   'Rail Unload'
		   END AS tserv_id,
		   se1.[placed_time] AS performed, 	
		   se1.[placed_by] AS move_recorded_by,
		   uv.[time_in] AS yard_in_time,
		   uv.[time_out] AS yard_out_time,
		   uv.[last_pos_loctype],
		   uv.[last_pos_locid],
		   mv.[fm_pos_loctype],
		   mv.[fm_pos_locid],
		   mv.[to_pos_loctype],
		   mv.[to_pos_locid],
		   ROUND(DATEDIFF(HOUR, uv.[time_in], uv.[time_out]) / 24.0, 2) AS dwell,
		   u.[pod1_id] AS pod,	   
		   CASE
			   WHEN ISNULL(uv.[arrive_pos_loctype], mv.[fm_pos_loctype]) = 'VESSEL' THEN
				   aivv.[vessel_service]
			   WHEN ISNULL(mv.[to_pos_loctype], uv.[last_pos_loctype]) = 'VESSEL' THEN
				   aovv.[vessel_service]
			   ELSE
				   NULL
		   END AS vessel_service,
		   CASE ISNULL(uv.[arrive_pos_loctype], mv.[fm_pos_loctype]) 
			   WHEN 'VESSEL' THEN 
				   aivv.[vessel_id] + aivv.[ib_vyg] 
			   ELSE
				   uv.[arrive_pos_locid]
		   END AS arrive_carrier,
		   CASE ISNULL(mv.[to_pos_loctype], uv.[last_pos_loctype])
			   WHEN 'VESSEL' THEN
				   aovv.[vessel_id] + aovv.[ob_vyg]
			   ELSE
				   uv.[last_pos_locid]
		   END AS depart_carrier,
		   IIF(gd.[temp_reqd_C] IS NULL, 'No', 'Yes') AS reefer_flg,
		   airv.[id] AS arrive_train_id,
		   ISNULL(aorv.[id], iorv.[id]) AS depart_train_id,
		   sva.[placed_time] AS rail_arrive_time,	 
		   svd.[placed_time] AS rail_depart_time,
		   CASE se1.[event_type_id]
			   WHEN 'UNIT_RAMP' THEN 
				   ISNULL(aorv.[railroad_line], iorv.[railroad_line])
			   WHEN 'UNIT_DERAMP' THEN
				   airv.[railroad_line]
		   END AS railroad_line,
		   uv.[arrive_pos_slot] AS arrival_position,
		   uv.[last_pos_slot] AS discharge_spot,
		   u.[depart_eqbo_nbr] AS booking,
		   gd.[bl_nbr] AS bill_of_lading,
		   ISNULL(aovv.[vessel_cutoff], iovv.[vessel_cutoff]) AS vessel_cutoff,
		   ISNULL(aovv.[vessel_1st_receiving], iovv.[vessel_1st_receiving]) AS vessel_1st_receiving,
		   mv.[pow]
	FROM   [SparcsN4R].[dbo].[inv_ar_unit]                   AS u     (NOLOCK)
	JOIN   [SparcsN4R].[dbo].[inv_ar_unit_fcy_visit]         AS uv    (NOLOCK) ON uv.[unit_gkey] = u.[gkey]
	JOIN   [SparcsN4R].[dbo].[srv_ar_event]                  AS se1   (NOLOCK) ON se1.[applied_to_class] = 'UNIT' AND
																				  se1.[placed_time] BETWEEN @dRunStartDt 
																									AND     @dRunEndDt AND
																				  se1.[applied_to_gkey] = u.[original_gkey] AND
																				  se1.[event_type_id] IN ('UNIT_RAMP', 'UNIT_DERAMP')
	JOIN   [SparcsN4R].[dbo].[inv_ar_move_event]             AS mv    (NOLOCK) ON mv.[ufv_gkey] = uv.[gkey] AND
																				  (mv.[fm_pos_loctype] IN ('TRAIN', 'RAILCAR') OR
																				   mv.[to_pos_loctype] IN ('TRAIN', 'RAILCAR')) AND
																				  mv.[arMve_gkey] = se1.[gkey]
	JOIN   [SparcsN4R].[dbo].[ref_equipment]                 AS e     (NOLOCK) ON e.[id_full] = u.[id] AND
																				  e.[scope] = 2
	JOIN   [SparcsN4R].[dbo].[ref_equip_type]                AS et    (NOLOCK) ON et.[gkey] = e.[eqtyp_gkey]
	LEFT   JOIN [SparcsN4R].[dbo].[inv_ar_goods]             AS gd    (NOLOCK) ON gd.[gkey] = u.[goods]
	LEFT   JOIN q_vessel_visits                              AS aivv           ON aivv.[gkey] = uv.[actual_ib_cv_gkey]
	LEFT   JOIN q_vessel_visits                              AS iovv           ON iovv.[gkey] = uv.[intend_ob_cv_gkey]
	LEFT   JOIN q_vessel_visits                              AS aovv           ON aovv.[gkey] = uv.[actual_ob_cv_gkey]
	LEFT   JOIN q_rail_visits                                AS airv           ON airv.[gkey] = uv.[actual_ib_cv_gkey]
	LEFT   JOIN q_rail_visits                                AS iorv           ON iorv.[gkey] = uv.[intend_ob_cv_gkey]
	LEFT   JOIN q_rail_visits                                AS aorv           ON aorv.[gkey] = uv.[actual_ob_cv_gkey]
	OUTER  APPLY (SELECT TOP 1 q1.[placed_time]
				  FROM   (SELECT se3.[placed_time]
						  FROM   [SparcsN4R].[dbo].[srv_event]           AS se3  (NOLOCK)
						  JOIN   [SparcsN4R].[dbo].[srv_event_types]     AS set3 (NOLOCK) ON set3.[gkey] = se3.[event_type_gkey] AND
																							 set3.[id] = 'UNIT_OUT_RAIL'
						  WHERE  se3.[applied_to_class] = se1.[applied_to_class]
						  AND    se3.[placed_time] BETWEEN @dRunStartDt
												   AND     @dRunEndDt
						  AND    se3.[applied_to_gkey] = u.[gkey]
						  UNION ALL
						  SELECT ase3.[placed_time]
						  FROM   [SparcsN4R].[dbo].[srv_ar_event]        AS ase3 (NOLOCK) 
						  WHERE  ase3.[applied_to_class] = se1.[applied_to_class] 
						  AND    ase3.[placed_time] BETWEEN @dRunStartDt
													AND     @dRunEndDt
						  AND    ase3.[applied_to_gkey] = u.[original_gkey] 
						  AND	 ase3.[event_type_id] = 'UNIT_OUT_RAIL') AS q1
				  ORDER  BY q1.[placed_time] DESC) AS svd
	OUTER  APPLY (SELECT TOP 1 q2.[placed_time]
				  FROM   (SELECT se4.[placed_time]
						  FROM   [SparcsN4R].[dbo].[srv_event]           AS se4  (NOLOCK)
						  JOIN   [SparcsN4R].[dbo].[srv_event_types]     AS set4 (NOLOCK) ON set4.[gkey] = se4.[event_type_gkey] AND
																							 set4.[id] = 'UNIT_IN_RAIL'
						  WHERE  se4.[applied_to_class] = se1.[applied_to_class]
						  AND    se4.[placed_time] BETWEEN @dRunStartDt
												   AND     @dRunEndDt
						  AND    se4.[applied_to_gkey] = u.[gkey]
						  UNION ALL
						  SELECT ase4.[placed_time]
						  FROM   [SparcsN4R].[dbo].[srv_ar_event]        AS ase4 (NOLOCK) 
						  WHERE  ase4.[applied_to_class] = se1.[applied_to_class] 
						  AND    ase4.[placed_time] BETWEEN @drunStartDt AND
															@dRunEndDt
						  AND    ase4.[applied_to_gkey] = u.[original_gkey] 
						  AND    ase4.[event_type_id] = 'UNIT_IN_RAIL') AS q2
				  ORDER  BY q2.[placed_time] DESC) AS sva
	WHERE  (u.[line_op_id] IN (SELECT [item] FROM @tLines) OR
			'ALL' IN (SELECT [item] FROM @tLines))
	ORDER  BY 12 -- performed;

END




GO


